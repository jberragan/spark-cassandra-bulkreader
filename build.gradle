buildscript {
  dependencies {
    classpath "com.github.jengelman.gradle.plugins:shadow:5.1.0"
  }
}

plugins {
  id 'java'
  id 'com.github.johnrengelman.shadow' version '5.1.0'
}

ext.crossbuildVersion = System.getenv("SCALA_VERSION") ?: "${scala}"
// sparkVersionQualifier adds the "-spark3" string to the artifact version, and for spark 2 artifacts
// the artifact version remains the same as before
ext.sparkbuildVersion = System.getenv("SPARK_VERSION") ?: "${spark}"
ext.sparkVersionQualifier = ext.sparkbuildVersion == "3" ? "-spark3" : ""

apply from: "profiles/scala-${ext.crossbuildVersion}${ext.sparkVersionQualifier}.gradle"

allprojects {
  apply plugin: 'idea'

  repositories{
    mavenCentral()
  }
  task copyCodeStyle(type: Copy) {
    from "ide/idea/codeStyleSettings.xml"
    into ".idea"
  }
  tasks.idea.dependsOn copyCodeStyle
  task copyInspections(type: Copy) {
    from "ide/idea/Project_Default.xml"
    into ".idea/inspectionProfiles"
  }
  tasks.idea.dependsOn copyInspections
}

subprojects {
  apply plugin: 'java'
  apply plugin: 'com.github.johnrengelman.shadow'

  idea {
    module {
      scopes.PROVIDED.plus += [configurations.shadow]
    }
  }

  tasks.withType(AbstractCompile) {
    classpath += configurations.shadow
  }

  sourceCompatibility = "${jdkLevel}"
  targetCompatibility = "${jdkLevel}"

  dependencies {
    implementation "com.intellij:annotations:${project.rootProject.intellijVersion}"
    implementation "net.java.dev.jna:jna:${project.rootProject.jnaVersion}"
  }

  sourceSets {
    main {
      java {
        if ("${project.rootProject.ext.sparkbuildVersion}" == "3") {
          srcDirs += ['src/main/spark3']
        } else {
          srcDirs += ['src/main/spark2']
        }
      }
    }
    test {
      java {
        if ("${project.rootProject.ext.sparkbuildVersion}" == "3") {
          srcDirs += ['src/test/spark3']
        } else {
          srcDirs += ['src/test/spark2']
        }
      }
    }
  }
}